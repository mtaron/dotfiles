# Use Ctrl+Z as interrupt instead of Ctrl+C
# This must be done before Powerlevel10k instant prompt
if [[ -o interactive ]]; then
  stty intr ^Z
else
  # .zshrc should never be loaded in a non-interactive shell,
  # but it's interesting that is mostly works with auto compinit disabled.
  ZGEN_AUTOLOAD_COMPINIT=0
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of .zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

has() {
  command -v "$1" 1>/dev/null 2>&1
}

# Incrementally add history to $HISTFILE instead of waiting for shell exit.
# This allows for running 'exec zsh' without losing history.
setopt inc_append_history

# Location for storing plugins
ZGEN_DIR="$XDG_CACHE_HOME/zgenom"

# Disable Oh-My-ZSH's internal updating
DISABLE_AUTO_UPDATE=true

ZSH_AUTOSUGGEST_STRATEGY=(completion)

ZSH_COMPLETIONS="$ZDOTDIR/completions"

# Load plugin manager
source "$XDG_DATA_HOME/zgenom/zgenom.zsh"

if ! zgenom saved; then
  zgenom ohmyzsh

  # Adds 'acs' alias that lists aliases grouped by plugin
  zgenom ohmyzsh plugins/aliases
  zgenom ohmyzsh plugins/copypath
  has direnv && zgenom ohmyzsh plugins/direnv
  has yarn && zgenom ohmyzsh plugins/yarn

  # Use 'acp' command to change profiles
  # https://github.com/ohmyzsh/ohmyzsh/tree/master/plugins/aws
  has aws && zgenom ohmyzsh plugins/aws

  has kubectl && zgenom ohmyzsh plugins/kubectx

  # SSH keys are subkeys of GPG
  # https://opensource.com/article/19/4/gpg-subkeys-ssh
  zgenom ohmyzsh plugins/gpg-agent

  # Fuzzy history search
  # https://github.com/junegunn/fzf
  zgenom ohmyzsh plugins/fzf

  zgenom load zsh-users/zsh-syntax-highlighting
  zgenom load zsh-users/zsh-autosuggestions

  zgenom load chrissicool/zsh-256color

  # Ctrl + Shift + Arrow to select by word
  zgenom load jirutka/zsh-shift-select

  zgen load hlissner/zsh-autopair

  zgenom load romkatv/powerlevel10k powerlevel10k

  zgenom load "$ZDOTDIR/zsh.d"

  # Completions
  has chezmoi && chezmoi completion zsh > "$ZSH_COMPLETIONS/_chezmoi"
  has gh && gh completion --shell zsh > "$ZSH_COMPLETIONS/_gh"
  has poetry && poetry completions zsh > "$ZSH_COMPLETIONS/_poetry"
  has kubectl && kubectl completion zsh > "$ZSH_COMPLETIONS/_kubectl"
  has k3d && k3d completion zsh > "$ZSH_COMPLETIONS/_k3d"
  has helm && helm completion zsh > "$ZSH_COMPLETIONS/_helm"
  [[ -d "$ZSH_COMPLETIONS" ]] && zgenom load "$ZSH_COMPLETIONS"

  zgenom save
fi

# This plugin apparently doesn't work when included in the zgenom save.
if has dotnet; then
  zgenom ohmyzsh plugins/dotnet
fi

# Azure CLI
if [[ -f /etc/bash_completion.d/azure-cli ]]; then
  autoload -U +X bashcompinit && bashcompinit
  source /etc/bash_completion.d/azure-cli
fi

argcomplete="$(command -v register-python-argcomplete || command -v register-python-argcomplete3)"
if has pipx && [[ -f $argcomplete ]]; then
  eval "$($argcomplete pipx)"
fi

# Dedupe paths using a ZSH builtin
# https://til.hashrocket.com/posts/7evpdebn7g-remove-duplicates-in-zsh-path
typeset -aU path fpath

[[ ! -f "$ZDOTDIR/.p10k.zsh" ]] || source "$ZDOTDIR/.p10k.zsh"
